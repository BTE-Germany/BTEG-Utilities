plugins {
    id 'java'
    id 'eclipse'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = 'dev.btedach'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url "https://m2.dv8tion.net/releases"
    }
    maven{
        url "https://ci.frostcast.net/plugin/repository/everything"
    }
    repositories {
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    compileOnly("com.velocitypowered:velocity-api:3.3.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.3.0-SNAPSHOT")
    implementation('org.json:json:20231013')
    compileOnly('net.luckperms:api:5.4')
    implementation('org.slf4j:slf4j-simple:2.0.0')
    implementation("net.dv8tion:JDA:5.0.0-alpha.19")
    compileOnly('org.projectlombok:lombok:1.18.30')
    annotationProcessor('org.projectlombok:lombok:1.18.30')
    compileOnly 'com.github.NEZNAMY:TAB-API:5.4.0'
}

def targetJavaVersion = 17

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = ['version': project.version]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}

sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })

project.idea.project.settings.taskTriggers.afterSync generateTemplates
project.eclipse.synchronizationTasks(generateTemplates)
